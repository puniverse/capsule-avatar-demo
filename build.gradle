apply plugin: 'java'

repositories {
	mavenCentral()
	maven { url "https://maven.java.net/content/groups/public/" }
}

configurations {
    capsule
    natives
}

dependencies {
	runtime "com.oracle:avatar-js:0.10.25-SNAPSHOT"
	natives "com.oracle:libavatar-js-linux-x64:0.10.25-SNAPSHOT"
	natives "com.oracle:libavatar-js-win-x64:0.10.25-SNAPSHOT"
	natives "com.oracle:libavatar-js-macosx-x64:0.10.25-SNAPSHOT"
	capsule "co.paralleluniverse:capsule:0.3.0"
}

task fullCapsule(type: Jar) {
    archiveName = "hello-nodejs.jar"
    
    from "app.js"
    from { configurations.runtime }
    from { configurations.natives } {
        rename 'libavatar-js-.+\\.(.+)', 'libavatar-js.$1'
    }
    
    from(configurations.capsule.collect { zipTree(it) }) { include 'Capsule.class' }
    
    manifest { 
        attributes(
	        "Main-Class"  : "Capsule",
	        "Application-Name" : "hello-nodejs",
	        "Application-Class" : "com.oracle.avatar.js.Server", 
            "Min-Java-Version" : "1.8.0",
            "Args" : '$CAPSULE_DIR/app.js',
        )
    }
}

task capsule(type: Jar) {
    archiveName = "hello-nodejs.jar"
    
    from "app.js"   
    from(configurations.capsule.collect { zipTree(it) })
    
    manifest { 
        attributes(
	        "Main-Class"  : "Capsule",
            "Application" : "com.oracle:avatar-js:0.10.25-SNAPSHOT",
            "Repositories" : "https://maven.java.net/content/groups/public/",
            "Native-Dependencies-Linux" : "com.oracle:libavatar-js-linux-x64:0.10.25-SNAPSHOT,libavatar-js.so",
            "Native-Dependencies-Win" : "com.oracle:libavatar-js-win-x64:0.10.25-SNAPSHOT,libavatar-js.dll",
            "Native-Dependencies-Mac" : "com.oracle:libavatar-js-macosx-x64:0.10.25-SNAPSHOT,libavatar-js.dylib",
            "Min-Java-Version" : "1.8.0",
            "Args" : '$CAPSULE_DIR/app.js',
        )
    }
}
